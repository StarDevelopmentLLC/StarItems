import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'xyz.kyngs.libby.plugin' version '2.0.1'
}

version = rootProject.version

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.stardevllc.com/releases'}
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'}
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'}
    maven { url = 'https://repo.codemc.org/repository/maven-public/'}
}

dependencies {
    implementation("com.stardevllc.libby:libby-bukkit:1.0.1")
    implementation project(':common')
    implementation "com.stardevllc:StarMCLib:${starmclibVersion}"
    libby "com.stardevllc:StarLib:${starlibVersion}"
    libby "com.stardevllc:ItemBuilder:${itembuilderVersion}"
    libby "com.stardevllc:SMaterial:${smaterialVersion}"
    compileOnly "org.spigotmc:spigot-api:${spigotVersion}"
    compileOnly "me.clip:placeholderapi:${papiVersion}"
    compileOnly "de.tr7zw:item-nbt-api-plugin:${nbtapiVersion}"

    libby "com.google.code.gson:gson:${gsonVersion}"
    libby "net.kyori:adventure-api:${adventureVersion}"
    libby "net.kyori:adventure-platform-bukkit:${adventureBukkitVersion}"
    libby "net.kyori:adventure-text-minimessage:${miniMessageVersion}"
    libby "org.yaml:snakeyaml:${snakeymlVersion}"
}

libby {
    excludeDependency("com.google.errorprone:.*:.*")
    excludeDependency("org.jetbrains:.*:.*")
}

processResources {
    filesMatching("*.yml") {
        expand(project.properties)
    }
}

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = 'standalone'
    archiveBaseName = 'StarItems'
    destinationDirectory = rootProject.layout.buildDirectory.dir("libs")

    relocate('net.kyori', 'com.stardevllc.kyori')
    relocate('com.google.gson', 'com.stardevllc.gson')
    relocate('org.yaml.snakeyaml', 'com.stardevllc.snakeyaml')

    relocate("net.byteflux.libby", "com.stardevllc.staritems.libby")
    relocate('com.grack', 'com.stardevllc.staritems.grack')
    relocate('com.stardevllc.config', 'com.stardevllc.staritems.config')
    relocate('com.stardevllc.smcversion', 'com.stardevllc.staritems.smcversion')
    relocate('com.stardevllc.starmclib', 'com.stardevllc.staritems.starmclib')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withJavadocJar()
    withSourcesJar()
}

tasks.compileJava.dependsOn(clean)
tasks.jar.finalizedBy(shadowJar)