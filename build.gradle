import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.2.2'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://www.jitpack.io'
    }
    
    maven {
        url = 'https://repo.stardevllc.com/releases'
    }
    
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://repo.codemc.org/repository/maven-public/'
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

group = 'com.stardevllc'
version = '0.3.1'

publishing {
    repositories {
        maven {
            name="stardev"
            url="https://repo.stardevllc.com/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    
    publications {
        maven(MavenPublication) {
            from components.shadow
            
            artifact ("build/libs/${project.name}-${project.version}-javadoc.jar") {
                classifier = "javadoc"
            }

            artifact ("build/libs/${project.name}-${project.version}-sources.jar") {
                classifier = "sources"
            }
        }
    }
}

dependencies {
    implementation project(':common')
    implementation "com.stardevllc:ItemBuilder:${itembuilderVersion}"
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
    compileOnly "com.stardevllc:StarMCLib:${starmclibVersion}"
    compileOnly "me.clip:placeholderapi:${papiVersion}"
    compileOnly "de.tr7zw:item-nbt-api-plugin:${nbtapiVersion}"
}

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = ''
}

processResources {
    filesMatching("*.yml") {
        expand(project.properties)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withJavadocJar()
    withSourcesJar()
}

tasks.named("javadoc", Javadoc) {
    subprojects { subproject ->
        source += subproject.sourceSets.main.allJava
    }
//    source += project.sourceSets.main.allJava

    subprojects { subproject ->
        classpath += subproject.sourceSets.main.compileClasspath
        classpath += subproject.sourceSets.main.runtimeClasspath
    }

//    classpath += project.sourceSets.main.compileClasspath
}

tasks.named('sourcesJar', Jar) {
    archiveClassifier.set("sources")
//    from sourceSets.main.allSource

    subprojects { subproject ->
        from subproject.sourceSets.main.allSource
    }
    
    exclude('plugin.yml')
}

tasks.compileJava.dependsOn(clean)
tasks.jar.finalizedBy(shadowJar)
tasks.build.finalizedBy(publishToMavenLocal)

tasks.withType(PublishToMavenLocal).configureEach {
    dependsOn(shadowJar)
    dependsOn(javadocJar)
    dependsOn(sourcesJar)
}

tasks.withType(PublishToMavenRepository).configureEach {
    dependsOn(shadowJar)
    dependsOn(javadocJar)
    dependsOn(sourcesJar)
}